# -*- mode: python; coding: utf-8-with-signature-unix -*-
#=======================================================================================================================

import os

outdir = '.obj/$TARGET_ARCH'

env = Environment(
    TARGET_ARCH = ARGUMENTS.get('TARGET_ARCH', 'x86'),
    MSVC_VERSION = '11.0',
    )
env.VariantDir(outdir, '.', duplicate=0)

pdb = 'main.pdb'
env.Append(
    CCFLAGS = [
        '/Zi',
        '/MD',
        '/EHsc',
        '/GR-',
        '/Od',
        '/Ob0',
        '/Fd' + pdb,
        '/W4',
        '/WX',
        ],
    CPPDEFINES = [
        '_MBCS',
        '_WINDOWS',
        'WIN32',
        '_DEBUG',
        ],
    LINKFLAGS = [
        '/DEBUG',
        '/INCREMENTAL:NO',
        '/SUBSYSTEM:CONSOLE',
        ],
    )

stdafx_pch, stdafx_obj = env.PCH(os.path.join(outdir, 'stdafx.cpp'))
env.Append(
    PCHSTOP = 'stdafx.h',
    PCH = stdafx_pch,
    CCFLAGS = [
        '/FI$PCHSTOP',
        ],
    )

main = env.Program(os.path.join(outdir, 'main.cpp'))
env.Clean(main, [pdb, outdir])
env.Alias('build', main)

env.Command('run', main, ['"$SOURCE"'])

